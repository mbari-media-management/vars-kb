plugins {
    id 'com.adarshr.test-logger' apply false
    id 'com.github.spotbugs' apply  false
    id 'org.kordamp.gradle.jdeps' apply false
    id "com.github.ben-manes.versions" apply false
    id "com.jfrog.bintray" apply false
    id "org.javamodularity.moduleplugin" apply false
}

subprojects {

    apply plugin: 'com.adarshr.test-logger'
    apply plugin: 'com.github.ben-manes.versions'
    apply plugin: 'com.github.spotbugs'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'org.javamodularity.moduleplugin'
    apply plugin: 'org.kordamp.gradle.jdeps'

    sourceCompatibility = 11
    targetCompatibility = 11

    version = '11.0.6.jre11'

    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()

        maven {
            url 'http://dl.bintray.com/org-mbari/maven'
        }
        maven {
            url 'http://dl.bintray.com/hohonuuli/maven'
        }
        maven {
            url 'http://dl.bintray.com/jerady/maven'
        }
    }

    test {
        useJUnitPlatform()
        moduleOptions {
            runOnClasspath = true
        }
        testlogger {
            theme 'mocha' // task level
        }
    }

    ext {
        javafxVersion = "14"
    }

    dependencies {
      constraints {
        implementation 'com.fatboyindustrial.gson-javatime-serialisers:gson-javatime-serialisers:1.1.1'
        implementation 'com.google.code.gson:gson:2.8.6'
        implementation 'com.google.guava:guava:28.2-jre'
        implementation 'com.google.inject:guice:4.2.2'
        implementation 'com.rabbitmq:amqp-client:5.8.0'
        implementation 'com.typesafe:config:1.4.0'
        implementation 'foxtrot:foxtrot-core:4.0'
        implementation 'io.reactivex.rxjava2:rxjava:2.2.19'
        implementation 'net.sourceforge.jtds:jtds:1.3.1'
        implementation 'org.apache.commons:commons-email:1.5'
        implementation 'org.bushe:eventbus:1.4'
        implementation 'org.eclipse.persistence:org.eclipse.persistence.jpa:2.7.6'
        implementation 'org.jasypt:jasypt:1.9.3'
        implementation 'org.mbari:mbarix4j:2.0.5.jre11'
        implementation 'org.mbari.m3:org.mbari.vars.core:0.3.20'
        implementation 'org.mbari.m3:org.mbari.vars.services:0.3.20'
        implementation 'org.mbari.vcr4j:vcr4j-core:4.3.2.jre11'
        implementation 'org.slf4j:slf4j-api:1.8.0-beta4'
        implementation 'org.swinglabs:swing-layout:1.0.3'
        implementation 'org.swinglabs.swingx:swingx-all:1.6.5'
        runtimeOnly 'ch.qos.logback:logback-classic:1.3.0-alpha4'
        runtimeOnly 'com.h2database:h2:1.4.200'
        runtimeOnly 'com.microsoft.sqlserver:mssql-jdbc:8.2.1.jre11'
        runtimeOnly 'com.oracle.database.jdbc:ojdbc10dms:19.6.0.0'
        // https://mvnrepository.com/artifact/com.oracle.ojdbc/dms
        runtimeOnly "com.oracle.ojdbc:dms:19.3.0.0"
        runtimeOnly 'net.sourceforge.jtds:jtds:1.3.1'
        runtimeOnly 'org.apache.derby:derby:10.15.2.0'
        runtimeOnly 'org.apache.derby:derbyclient:10.15.2.0'
        runtimeOnly 'org.apache.derby:derbynet:10.15.2.0'
        runtimeOnly 'org.apache.derby:derbyshared:10.15.2.0'
        runtimeOnly 'org.apache.derby:derbytools:10.15.2.0'
        runtimeOnly 'org.fusesource.jansi:jansi:1.18'
        runtimeOnly 'org.postgresql:postgresql:42.2.11'
      }

      testCompileOnly 'junit:junit:4.13'
      testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
      testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.0'
      testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.6.0'
    }

    task tree(type: DependencyReportTask) {}

    spotbugs {
        ignoreFailures = true
    }

    jdepsReport {
        classpaths = ['compileClasspath', 'runtimeClasspath']
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId "org.mbari.m3"
                artifactId project.name
                from components.java
            }
        }
    }

    bintray {
        key = System.getenv("BINTRAY_KEY")
        user = System.getenv("BINTRAY_USER")
        publications = ['maven']
        pkg {
            repo = 'maven'
            name = project.name
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/mbari-media-management/vars-kb.git'
        }
    }

}
