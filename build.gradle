plugins {
    id 'com.adarshr.test-logger' apply false
     id 'com.github.spotbugs' apply  false
    id 'org.kordamp.gradle.jdeps' apply false
    id "com.github.ben-manes.versions" apply false
    // id "com.jfrog.bintray" apply false
    id "org.javamodularity.moduleplugin" apply false
}

subprojects {

    apply plugin: 'com.adarshr.test-logger'
    apply plugin: 'com.github.ben-manes.versions'
    // apply plugin: 'com.github.spotbugs'
    // apply plugin: 'com.jfrog.bintray'
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'org.javamodularity.moduleplugin'
    apply plugin: 'org.kordamp.gradle.jdeps'

    sourceCompatibility = 21
    targetCompatibility = 21

    version = '11.1.0'

    repositories {
        // jcenter()
        mavenCentral()
        // mavenLocal()

        maven {
            name = "MBARI"
            url = uri("https://maven.pkg.github.com/mbari-org/maven")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
        // maven {
        //     url 'http://dl.bintray.com/jerady/maven'
        // }
    }

    configurations {
        testCompile
    }

    test {
        useJUnitPlatform()
        moduleOptions {
            runOnClasspath = true
        }
        testlogger {
            theme 'mocha' // task level
        }
    }

    ext {
        javafxVersion = "21.0.1"
    }

    dependencies {
        constraints {
            implementation 'com.fatboyindustrial.gson-javatime-serialisers:gson-javatime-serialisers:1.1.1'
            implementation 'com.fasterxml.jackson.core:jackson-core:2.16.2'
            implementation 'com.fasterxml.jackson.core:jackson-databind:2.16.2'
            implementation 'com.google.code.gson:gson:2.10.1'
            implementation 'com.google.guava:guava:33.0.0-jre'
            implementation 'com.google.inject:guice:5.1.0'
            implementation 'com.rabbitmq:amqp-client:5.17.0'
            implementation 'com.typesafe:config:1.4.3'
            implementation 'foxtrot:foxtrot-core:4.0'
            implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
            implementation 'jakarta.activation:jakarta.activation-api:2.1.1'
            implementation 'net.sourceforge.jtds:jtds:1.3.1'
            implementation 'org.apache.commons:commons-email:1.5'
            implementation 'org.bushe:eventbus:1.4'
            implementation 'org.eclipse.persistence:org.eclipse.persistence.jpa:2.7.13'
            implementation 'org.jasypt:jasypt:1.9.3'
            implementation 'org.mbari:mbarix4j:2.0.5.jre11'
            implementation 'org.mbari.vars:org.mbari.vars.core:2.0.5'
            implementation 'org.mbari.vars:org.mbari.vars.services:2.0.5'
            implementation 'org.mbari.vcr4j:vcr4j-core:4.4.1.jre11'
            implementation 'org.slf4j:slf4j-api:2.0.12'
            implementation 'org.swinglabs:swing-layout:1.0.3'
            implementation 'org.swinglabs.swingx:swingx-all:1.6.5-1'
            runtimeOnly 'ch.qos.logback:logback-classic:1.5.3'
            runtimeOnly 'com.h2database:h2:2.0.202'
            runtimeOnly 'com.microsoft.sqlserver:mssql-jdbc:12.6.1.jre11'
            runtimeOnly 'com.oracle.database.jdbc:ojdbc11:21.1.0.0'
            runtimeOnly 'net.sourceforge.jtds:jtds:1.3.1'
            runtimeOnly 'org.apache.derby:derby:10.17.1.0'
            runtimeOnly 'org.apache.derby:derbyclient:10.17.1.0'
            runtimeOnly 'org.apache.derby:derbynet:10.17.1.0'
            runtimeOnly 'org.apache.derby:derbyshared:10.17.1.0'
            runtimeOnly 'org.apache.derby:derbytools:10.17.1.0'
            runtimeOnly 'org.fusesource.jansi:jansi:2.4.0'
            runtimeOnly 'org.postgresql:postgresql:42.7.2'
        }

        testCompileOnly 'junit:junit:4.13.2'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
        testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.10.2'
    }

    task tree(type: DependencyReportTask) {}

    // spotbugs {
    //     ignoreFailures = true
    // }

    jdepsReport {
        classpaths = ['compileClasspath', 'runtimeClasspath']
    }

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/mbari-org/maven")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                    password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
                }
            }
        }
        publications {
            gpr(MavenPublication) {
                groupId "org.mbari.vars"
                artifactId project.name
                from components.java
            }
        }
    }


}
